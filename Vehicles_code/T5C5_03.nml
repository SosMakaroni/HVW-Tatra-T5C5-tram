// --------------------------------------------------------------------------------T5C5_03

// ss=spriteset, sg=spritegroup, sw=switch
// tmpl_1(szin,csuklo,rakodas,poziciok)
// tmpl_depo(szin,csuklo)

spriteset(ss_T5C5_03_purchase, "src/T5C5a_03_depo.png")		{tmpl_purchase()}
//--------------A
spriteset(ss_T5C5a_03_depo, "src/T5C5a_03_depo.png")		{tmpl_depo()}
spriteset(ss_T5C53a_01, "src/T5C5a_01_1x_8bpp.png")		{tmpl_1(1)}
spriteset(ss_T5C53a_02, "src/T5C5a_02_1x_8bpp.png")		{tmpl_1(1)}
spriteset(ss_T5C53a_03, "src/T5C5a_03_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_T5C53a_01, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/T5C5a_01_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_T5C53a_02, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/T5C5a_02_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_T5C53a_03, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/T5C5a_03_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_T5C53a_01, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/T5C5a_01_4x_8bpp.png") {tmpl_1(4)}
alternative_sprites(ss_T5C53a_02, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/T5C5a_02_4x_8bpp.png") {tmpl_1(4)}
alternative_sprites(ss_T5C53a_03, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/T5C5a_03_4x_8bpp.png") {tmpl_1(4)}

spritegroup sg_T5C53a_01 {	loaded:  [ss_T5C53a_01];	loading: [ss_T5C53a_01];}
spritegroup sg_T5C53a_02 {	loaded:  [ss_T5C53a_02];	loading: [ss_T5C53a_02];}
spritegroup sg_T5C53a_03 {	loaded:  [ss_T5C53a_03];	loading: [ss_T5C53a_03];}

switch (FEAT_ROADVEHS, SELF, sg_T5C5a_03_depo, position_in_consist ) {
    1..5: ss_toldat;
    ss_T5C5a_03_depo;
}
switch (FEAT_ROADVEHS, SELF, sw_T5C5a_03, position_in_consist ) {
    1: ss_toldat;
	2: sg_T5C53a_02;
	3: ss_toldat;
	4: sg_T5C53a_03;
	5: ss_toldat;
    sg_T5C53a_01;
}
switch(FEAT_ROADVEHS, SELF, sw_T5C5a_03_ss, (extra_callback_info1 & 0x12)) {
   0x10..0x12: sg_T5C5a_03_depo; // 0x10 Depot GUI, 0x11 Vehicle Details/Refit GUI, 0x12 Vehicle List
   0x00: sw_T5C5a_03; // 0x00 Vehicle in game
}

switch (FEAT_ROADVEHS, SELF, sw_T5C5_03_ss, cargo_subtype) {
	0: sw_T5C5a_03_ss;
	CB_RESULT_NO_TEXT;
}
switch (FEAT_ROADVEHS, SELF, sw_T5C5_03_articulated, extra_callback_info1) {
    0..5: item_T5C5_03;
    CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch (FEAT_ROADVEHS, SELF, sw_T5C5_03_capacity, position_in_consist) {
	1: return 0;
	2: return 100;
	3: return 0;
	4: return 100;	
	5: return 0;
	return 100;
}
switch (FEAT_ROADVEHS, SELF, sw_T5C5_03_length, position_in_consist) {
	1: return 6;
	2: return 8;
	3: return 6;
	4: return 7;
	5: return 8;
    return 8;
}
switch (FEAT_ROADVEHS, SELF, sw_T5C5_03_livery_text, cargo_subtype) {
	0: return string(STR_COLOUR_SCHEME_BUDAPEST);
	CB_RESULT_NO_TEXT;
}


item(FEAT_ROADVEHS, item_T5C5_03) {
	property {
        name:								string(STR_T5C5_03_NAME);
        climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:				date(1984,09,01);
        model_life:						20;
        vehicle_life:						50;
        reliability_decay:				10;
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					117; //9*13
        cost_factor:						105;
        running_cost_factor:				126;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        speed:								65 km/h;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_ROADVEH;
        power:								540 kW;
        weight:							57 ton;
        cargo_capacity:					10;
		cargo_age_period:					30;
        sound_effect:						SOUND_ELECTRIC_SPARK;
    }
    graphics {
		default:                 		sw_T5C5_03_ss;
		purchase:						ss_T5C5_03_purchase;
		articulated_part:        		sw_T5C5_03_articulated;
		length:                  		sw_T5C5_03_length;
		cargo_subtype_text: 			sw_T5C5_03_livery_text;
		cargo_capacity: 				sw_T5C5_03_capacity;
		purchase_cargo_capacity:		return 50;
		additional_text: 				string(STR_T5C5_03_ADAT);
    }
}
// --------------------------------------------------------------------------------T5C5_03 End
